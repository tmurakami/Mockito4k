buildscript {
    ext.kotlin_version = '1.1.4'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.13'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.ben-manes.versions'

apply from: "$rootDir/gradle/version.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

group = 'com.github.tmurakami'
version = version_name

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/mockito/maven' }
}

configurations {
    ktlint
}

def test_kotlin_version = project.properties['testKotlinVersion'] ?: kotlin_version
def test_mockito_version = project.properties['testMockitoVersion'] ?: 'latest.release'
def test_mockito_type = project.properties['testMockitoType'] ?: 'core'

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.3'
    compile 'org.mockito:mockito-core:2.7.0'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:1.0.7'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-${test_mockito_type}:$test_mockito_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$test_kotlin_version"
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['src/**/*.kt']
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['-F', 'src/**/*.kt']
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    skipDeprecated = true
    outputDirectory = javadoc.destinationDir
    processConfigurations = ['compile', 'compileOnly']
}
javadoc.dependsOn dokka as Object

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin.kotlinOptions {
    apiVersion = languageVersion = '1.0'
}
compileTestKotlin.kotlinOptions {
    apiVersion = languageVersion = (test_kotlin_version =~ /\d+\.\d+/)[0]
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}
