buildscript {
    ext.kotlin_version = '1.1.3-2'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.13'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.ben-manes.versions'

apply from: "$rootDir/gradle/version.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

group = 'com.github.tmurakami'
version = version_name

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.3'
    compile 'org.mockito:mockito-core:2.7.0'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:1.0.7'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:${project.properties['testKotlinVersion'] ?: kotlin_version}"
}

task ktlint(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['src/**/*.kt']
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args = ['-F', 'src/**/*.kt']
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    skipDeprecated = true
    outputDirectory = javadoc.destinationDir
    processConfigurations = ['compile', 'compileOnly']
}
javadoc.dependsOn dokka

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin.kotlinOptions.languageVersion = '1.0'
compileTestKotlin.kotlinOptions.freeCompilerArgs += '-Xskip-runtime-version-check'
